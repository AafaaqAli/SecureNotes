plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}



android {
    /**
     * REMINDER: Use local.properties to store credentials and access as 'Properties()'
     * */
    def KEY_STORE_PASSWORD
    def KEY_STORE
    def KEY_STORE_ALIAS

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    if (rootProject.file("local.properties").exists()) {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        KEY_STORE_PASSWORD = properties.getProperty('KEY_PASSOWORD')
        KEY_STORE = properties.getProperty('KEY_STORE')
        KEY_STORE_ALIAS = properties.getProperty('KEY_ALIAS')
    }

    signingConfigs {
        release {
            storeFile file(KEY_STORE)
            storePassword KEY_STORE_PASSWORD
            keyAlias KEY_STORE_ALIAS
            keyPassword KEY_STORE_PASSWORD
        }
    }

    /**
    * TODO: disable Crashlytics on debug when linked
    * */
    buildTypes {
        debug {
            debuggable true
            crunchPngs true
            shrinkResources false
            minifyEnabled false
            manifestPlaceholders = [applicationLabel :"@string/app_name_debug"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        qa {
            debuggable false
            crunchPngs false
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders = [applicationLabel :"@string/app_name_qa"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        release {
            debuggable false
            crunchPngs false
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders = [applicationLabel :"@string/app_name"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    /**
     * TODO: For later use for premium functionalities
     * */
    flavorDimensions 'tier'
    productFlavors {
        donate {
            dimension 'tier'
        }

        free {
            dimension 'tier'
            matchingFallbacks = ['premium']
        }
    }


    compileSdk 32

    defaultConfig {
        applicationId "com.aafaq.SecureNotes"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "SecureNotes-$versionCode")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Compose dependencies
    implementation "androidx.navigation:navigation-compose:2.5.0-alpha03"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"


    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:2.39.1"
    kapt "com.google.dagger:hilt-android-compiler:2.39.1"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // Room
    implementation "androidx.room:room-runtime:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"

    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:2.4.2"

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    //Multi density
    implementation 'com.intuit.ssp:ssp-android:1.0.6'
    implementation 'com.intuit.sdp:sdp-android:1.0.6'

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.1"

    //Gson
    implementation 'com.google.code.gson:gson:2.9.0'
}